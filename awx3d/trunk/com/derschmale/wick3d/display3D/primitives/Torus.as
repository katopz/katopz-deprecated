/*
Copyright (c) 2008 David Lenaerts.  See:
    http://code.google.com/p/wick3d
    http://www.derschmale.com

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*/

package com.derschmale.wick3d.display3D.primitives
{
	import com.derschmale.wick3d.core.geometry.Vertex3D;
	import com.derschmale.wick3d.core.imagemaps.UVCoords;
	import com.derschmale.wick3d.core.objects.Model3D;
	import com.derschmale.wick3d.materials.AbstractMaterial;
	
	/**
	 * The Torus class is a Model3D containing a Torus, a 3-dimensional object generated by revolving a circle about the y-axis, resembling a doughnut.
	 * 
	 * @author David Lenaerts
	 */
	public class Torus extends Model3D
	{
		/**
		 * Creates a Torus instance.
		 * 
		 * @param material The material used to render this object
		 * @param tubeRadius The radius of the tube.
		 * @param tubeDistance The distance of the tube's center to the center of the object; the radius of the tubes center circle.
		 * @param uSegments The amount of segments out of which the torus is constructed along the tube's central circle
		 * @param vSegments The amount of segments out of which the torus is constructed along the y-axis 
		 */
		public function Torus(material : AbstractMaterial, tubeRadius : Number, tubeDistance : Number, uSegments : uint = 15, vSegments : uint = 8)
		{
			var stepU : Number = 2*Math.PI/uSegments;
			var stepV : Number = 2*Math.PI/vSegments;
			var v1 : Vertex3D, v2 : Vertex3D, v3 : Vertex3D;
			var uv1 : UVCoords, uv2 : UVCoords, uv3 : UVCoords;
			var u : Number, v : Number;
			super(material);
			
			for (var uLoop : Number = 0; uLoop < uSegments; uLoop++) {
				for (var vLoop : Number = 0; vLoop < vSegments; vLoop++) {
					u = uLoop*stepU;
					v = vLoop*stepV;
					v1 = new Vertex3D(	(tubeDistance+tubeRadius*Math.cos(v))*Math.cos(u),
										(tubeDistance+tubeRadius*Math.cos(v))*Math.sin(u),
										tubeRadius*Math.sin(v)
									 );
					v2 = new Vertex3D(	(tubeDistance+tubeRadius*Math.cos(v+stepV))*Math.cos(u),
										(tubeDistance+tubeRadius*Math.cos(v+stepV))*Math.sin(u),
										tubeRadius*Math.sin(v+stepV)
									 );
					v3 = new Vertex3D(	(tubeDistance+tubeRadius*Math.cos(v))*Math.cos(u+stepU),
										(tubeDistance+tubeRadius*Math.cos(v))*Math.sin(u+stepU),
										tubeRadius*Math.sin(v)
									 );
					
					uv1 = new UVCoords(1-uLoop/uSegments, vLoop/vSegments);
					uv2 = new UVCoords(1-uLoop/uSegments, (vLoop+1)/vSegments);
					uv3 = new UVCoords(1-(uLoop+1)/uSegments, vLoop/vSegments);
					
					addTriangle(v1, v2, v3, uv1, uv2, uv3);
					
					v1 = new Vertex3D(	(tubeDistance+tubeRadius*Math.cos(v))*Math.cos(u+stepU),
										(tubeDistance+tubeRadius*Math.cos(v))*Math.sin(u+stepU),
										tubeRadius*Math.sin(v)
									 );
					v2 = new Vertex3D(	(tubeDistance+tubeRadius*Math.cos(v+stepV))*Math.cos(u),
										(tubeDistance+tubeRadius*Math.cos(v+stepV))*Math.sin(u),
										tubeRadius*Math.sin(v+stepV)
									 );
					v3 = new Vertex3D(	(tubeDistance+tubeRadius*Math.cos(v+stepV))*Math.cos(u+stepU),
										(tubeDistance+tubeRadius*Math.cos(v+stepV))*Math.sin(u+stepU),
										tubeRadius*Math.sin(v+stepV)
									 );
					
					uv1 = new UVCoords(1-(uLoop+1)/uSegments, vLoop/vSegments);
					uv2 = new UVCoords(1-uLoop/uSegments, (vLoop+1)/vSegments);
					uv3 = new UVCoords(1-(uLoop+1)/uSegments, (vLoop+1)/vSegments);
					
					addTriangle(v1, v2, v3, uv1, uv2, uv3);
				}
			}
		}
	}
}